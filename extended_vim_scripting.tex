% vim: ts=4 sts=4 sw=4 tw=80
\chapter{高级 Vim 脚本编程}
\label{chap:extended_vim_scripting}
\marginpar{175}

在前面一章, 我们已经学习了开发 Vim 脚本的基础知识, 现在, 我们将要把前面所学的
知识融会贯通, 按照结构化的方法把它们组织起来, 并对脚本进行测试.

这一章涵盖的主题包括:
\begin{itemize}
    \item 如何组织 Vim 脚本的结构
    \item Vim 脚本开发的一些技巧
    \item 如何调试 Vim 脚本
    \item 如何在 Vim 脚本中使用其他脚本语言
\end{itemize}

阅读完这一章之后, 读者将有能力运用 Vim 脚本语言与其他脚本语言开发出自己的脚本.
也就是说, 读者将有能力扩展 Vim 的功能.

\section{脚本结构}
\label{sec:script_structure}

前面我们已经介绍了 Vim 脚本的各个要素, 现在我们需要知道如何把它们组织在一起,
从而形成一个完成的脚本.

在大部分情况下, Vim 脚本仅由单个文件组成, 因此这一章的示例也仅限于单个文件. 我
们还打算让其他人能够获取到脚本, 因此需要保证代码的可读性.

在下面的几节里, 我们将会逐个介绍一个格式良好的脚本的各个要素.
\marginpar{176}

\subsection{脚本头部}
\label{subsec:script_header}

一个脚本文件在开头最好配上一个头部信息, 用来写明该脚本的用途. 头部应该包含下
面这些信息:
\begin{itemize}
    \item 脚本的维护人员
    \item 最后一次更新的版本
    \item 发布许可证 (最重要的信息)
\end{itemize}
一个示例是:
\begin{vimcode}
" myscript.vim  : Example script to show how a script is structured.
" Version       : 1.0.5
" Maintainer    : Kim Schulz<kim@schulz.dk>
" Last modified : 01/01/2007
" License       : This script is released under the Vim License.
\end{vimcode}
注意头部信息都是以 \texttt{"} 开始的, 也就是说它们都是一些注释.

头部还可以包含其他信息, 比如脚本可能依赖于其他脚本, 或者是该脚本对 Vim 版本
的要求.

\subsection{脚本加载检查}
\label{subsec:script_loaded_check}

一种良好的编程习惯是检查脚本是否已被加载, 如果是, 则在继续其他操作之前, 先执
行卸载操作.
% It is always a good practice to check if the script has already been loaded
% once, and if it has, then unload functions before moving on.
这是因为脚本不仅会安装在系统的全局目录中, 还会安装在用户的
\texttt{VIMHOME} 目录下.

检查脚本是否已加载的函数可以这样实现:
\begin{vimcode}
if exists("loaded_myscript")
    finish "stop loading the script
endif
let loaded_myscript = 1
\end{vimcode}
如果脚本未被加载, \texttt{if} 条件判断为假, 因此函数设置变量
\texttt{loaded\_myscript}.

当下一次加载脚本时, \texttt{if} 条件判断为真, 因为此时变量
\texttt{loaded\_myscript} 已经存在, 然后函数停止加载脚本.
\marginpar{177}

在某些情况下, 停止加载脚本可能并不是最好的做法, 因为用户可能修改了
\texttt{VIMHOME} 目录中的脚本版本. 所以, 这时候应该先卸载脚本, 然后再重新加载
脚本. 完成这项功能的函数可以这样实现:
\begin{vimcode}
if exists("loaded_myscript")
    delfunction MyglobalfunctionB
    delfunction MyglobalFunctionC
endif
let loaded_myscript = 1
\end{vimcode}

脚本开发人员无法知道 Vim/vi 当前是否处于兼容模式下 (如果是 vi 的话, 则比较有可
能), 所以比较好的做法是在脚本中保存编辑器的兼容模式. 这样做就可以确保脚本可以
正常地使用 Vim 特定的功能. 把下面的代码加到加载检查语句的后面:
\begin{vimcode}
:let s:global cpo = &cpo "store current compatible-mode
                         " in local variable
:set cpo&vim             " go into nocompatible-mode
\end{vimcode}
最后在脚本的末尾恢复原来的兼容模式:
\begin{vimcode}
:let &cpo = s:global_cpo
\end{vimcode}

\subsection{脚本配置}
\label{subsec:script_configuration}

用户在阅读别人开发的脚本时, 总是从头开始看起, 所以我们最好把所有的配置选项都
放在脚本的开始. 配置选项可以是外部程序的路径, 脚本依赖的特定文件的名字, 文件
类型等.

用户可能会在他的 \texttt{vimrc} 文件中改变 Vim 的配置, 所以开发人员需要确保
脚本不会覆盖掉他原来的配置. 方法是事先检查配置是否已被设置过, 只有在没有设置
过的情况下才设置它.

脚本中的设置语句可以这样写:
\begin{vimcode}
" variable myscript_path
if !exists("myscript_path")
    let s:vimhomepath = split(&runtimepath, ',')
    let s:myscript_path = s:vimhomepath[0]."/plugin/myscript.vim"
else
    let s:myscript_path = myscript_path
    unlet myscript_path
\end{vimcode}
\marginpar{178}
\begin{vimcode}
endif

" variable myscript_indent
if !exists("myscript_indent")
    let s:myscript_indent = 4
else
    let s:myscript_indent = myscript_indent
    unlet myscript_indent
endif
\end{vimcode}
上面的例子设置了两个配置变量 --- \texttt{myscript\_path} 与
\texttt{myscript\_indent}. 我们查找变量是否已存在, 如果不存在, 则在脚本的作用
域内设置变量的默认值 (比如, \texttt{s:myscript\_path}).

如果用户已经设置了变量, 那么变量的值就赋给脚本作用域内的同名变量.

最终, 用户定义的变量用 \texttt{unlet} 移除, 这样的话, 它就不对全局作用域产生
影响 --- 配置只需要在脚本内起作用即可.

\subsection{按键映射}
\label{subsec:key_mappings}

如果需要的话, 还可以添加按键映射, 这些映射可以是函数调用, 变量设置等. 和配置
变量一样, 在设置某个映射之前, 需要检查一下该映射是否已经建立好了, 检查的语句
可以这样写:
\begin{vimcode}
if !hasmapto('<Plug>MyscriptMyfunctionA')
    map <unique> <Leader>a <Plug>MyscriptMyfunctionA
endif
\end{vimcode}
在上面的代码中包含了一些我们以前没有介绍过的东西:
\begin{itemize}
    \item \texttt{hasmapto()}: 用于检查某个函数映射是否存在的函数
    \item \texttt{<unique>}: 如果存在相同的映射存在, 则报错.
    \item \texttt{<Leader>}: 由用户决定使用哪个映射前导字符. \texttt{<Leader>}
        将会被全局变量 \texttt{mapleader} 的值所替换.
    \item \texttt{<Plug>}: 为一个函数建立一个唯一的全局标识符, 这样的话, 它就
        不会与全局作用域中的其他函数产生冲突.
\end{itemize}
\marginpar{179}
把这些元素都组织在一起之后, 我们就创建一个脚本, 用来检查是否存在某个映射, 已经
绑定到唯一的函数标识 \texttt{<Plug>MyscriptMyfunctionA} 上. 如果这样的映射不存
在, 就把 \texttt{<Leader>a} 映射到标识符上 --- 除非 \texttt{<Leader>a} 已经被
其他人占用了, 此时 Vim 就会报错.

用户可能好奇 Vim 是如何从 \texttt{<Plug>MyscriptMyfunctionA} 得到
\texttt{MyfunctionA()} 的. 为此, 我们还需要建立其他一些映射:
\begin{vimcode}
noremap <unique> <script> <Plug>MyscriptMyfunctionA <SID>MyfunctionA
noremap <SID>MyfunctionA :call <SID>MyfunctionA()<CR>
\end{vimcode}
第一个命令把 \texttt{<Plug>MyscriptMyfunctionA} 映射到 \texttt{<SID>MyfunctionA}
上. 我们在代码中用到了 \texttt{<SID>}, 这是因为这个小标签会被 Vim 为当前脚本所
生成的唯一 ID 给替换掉. 如果我们想制作一个全局的函数映射, 并且只在当前脚本作
用域内可用 (例如 \texttt{s:MyfunctionA()}), 就需要这样的技术.

第二个命令把真正的函数 (\texttt{<SID>MyfunctionA()}, 也就是
\texttt{s:MyfunctionA()}) 绑定到 \texttt{<SID>MyfunctionA}.

设置完毕后, 当用户按下 \verb'\a' (把 \texttt{mapleader} 设置成 \verb'\'), 第
一个映射把它翻译成 \texttt{<Plug>MyscriptMyfunctionA}, 它在脚本内定义, 因此
\texttt{<SID>} 的值是正确的. 因此, \texttt{<Plug>MyscriptMyfunctionA} 再次
被翻译成 \texttt{<SID>MyfunctionA}, 最终被映射到调用本地函数
\texttt{s:MyfunctionA()}.

读者可能觉得上面所说的有点复杂, 而且 \texttt{MyfunctionA()} 可能本来就是一个
全局唯一的函数. 但是, 如果函数名是一些常见的名字, 比如 \texttt{Add()},
\texttt{Delete()}, \texttt{Convert()} 等, 其他脚本很可能也实现了这些函数.
在这些情况下, 这些冲突的函数名会让 Vim 无法判断到底应该使用哪个函数. 当然,
用户可以给自己的函数取一些怪异的名字, 从而避免重名, 但是这种做法会让自己的代码
变得杂乱无章, 而且还会污染全局作用域.

\begin{warning}
更多的信息请参考:
\begin{vimcode}
:help <SID>
:help <Plug>
:help 'script-local'
\end{vimcode}
\end{warning}
\marginpar{180}

\subsection{函数}
\label{subsec:functions}

函数几乎可以算作脚本开发过程中最重要的部分. 我们已经看到了如何创建一个函数,
而且已经注意到, 用作用域标记 \texttt{s:} 把函数的作用域限定在脚本内, 通常要比
全局范围内可见要好一点. 函数的一个例子是:
\begin{vimcode}
" this is our local function with a mapping
function s:MyfunctionA()
    echo "this is the script-scope function MyfunctionA speaking"
endfunction

" this is a global function which can be called by anyone
function MyglobalfunctionB()
    echo "Hello from the global-scope function myglobalfunctionB"
endfunction

" this is another global function which can be called by anyone
function MyglobalfunctionC()
    echo "Hello from MyglobalfuncionC() now calling locally:"
    call <SID>MyfunctionA()
endfunction
\end{vimcode}
第一个函数是私有函数, 只有在脚本内可见, 另外两个函数的作用域是全局的. 需要注意
的是, 因为全局函数知道当前脚本的 \texttt{<SID>}, 所以它们可以调用本地函数.

\subsection{一个完整的脚本}
\label{subsec:putting_it_all_together}

如果读者已经看过前面几节, 那么现在读者手上就已经具备了构成一个完整的脚本的全部
要素.

现在, 让我们把所有的要素都集中在一起, 看一下一个完整的脚本是什么样子的:
\begin{vimcode}
" myscript.vim  - Example script to show how a script is structured.
" Version       : 1.0.5
" Maintainer    : Kim Schulz<kim@schulz.dk>
" Last modified : 01/01/2007
" License       : This script is released under the Vim License.

" check if script is already loaded
if exists("loaded_myscript")
	finish "stop loading the script
endif
let loaded_myscript=1
\end{vimcode}
\marginpar{181}
\begin{vimcode}

	let s:global_cpo = &cpo  "store compatible-mode in local variable
	set cpo&vim              " go into nocompatible-mode
" ######## CONFIGURATION ########
" variable myscript_path
if !exists("myscript_path")
	let s:vimhomepath = split(&runtimepath,',')
	let s:myscript_path = s:vimhomepath[0]."/plugin/myscript.vim"
else
	let s:myscript_path = myscript_path
	unlet myscript_path
endif

" variable myscript_indent
if !exists("myscript_indent")
	let s:myscript_indent = 4
else
	let s:myscript_indent = myscript_indent
	unlet myscript_indent
endif

	" ######## FUNCTIONS #########
	" this is our local function with a mapping
function s:MyfunctionA()
	echo "This is the script-scope function MyfunctionA speaking"
endfunction

" this is a global function which can be called by anyone
function MyglobalfunctionB()
	echo "Hello from the global-scope function myglobalfunctionB"
endfunction
" this is another global function which can be called by anyone
function MyglobalfunctionC()
	echo "Hello from MyglobalfuncionC() now calling locally:"
	call <SID>MyfunctionA()
endfunction

" return to the users own compatible-mode setting
:let &cpo = s:global_cpo
\end{vimcode}
\marginpar{182}
现在, 你手上就已经有了一个完整的脚本, 这是我们的第一个 Vim 脚本插件.  虽然它的功
能还不是很丰富, 但是它已经展现了一个 Vim 脚本的完整结构. Vim 还有其他几种类型的
脚本, 比如文件类型插件, 编译器插件, 和库函数脚本, 关于如何编写这些脚本, 可以
参考:
\begin{vimcode}
:help 'write-filetype-plugin'
:help 'write-compiler-plugin'
:help 'write-library-script'
\end{vimcode}

\begin{warning}
	在 Vim 官网 \url{http://www.vim.org} 上可以找到大量的脚本, 读者可以从中获
	取灵感. 其中一些脚本属于库函数, 它们可以加快脚本的开发过程.
\end{warning}

\section{脚本开发技术}
\label{sec:scripting_tips}

在这一节, 我们将会介绍一些脚本开发过程中的小技巧. 有些技巧非常简单, 例如可以
直接插入到脚本的一小段代码, 还有一些则非常值得一学.

\subsection{Gvim 或 Vim}
\label{subsec:gvim_vim}

有些脚本在 Gvim 中使用时会有一些额外的特性. 包括添加菜单, 工具栏, 或者是其他
一些只能在 Gvim 使用的功能. 那么, 开发人员应该如何知道用户现在是工作在 Vim
还是 Gvim 下呢? 其实 Vim 已经提前准备好了这些信息, 开发人员所要做的仅仅是检查
特性 \texttt{gui\_running} 是否已经开启. 为了完成这个操作, 需要使用一个函数
\texttt{has()}, 如果指定的特性是支持的, 则返回 \texttt{1}, 否则的话, 为
\texttt{0}.

一个例子是:
\begin{vimcode}
if has("gui_running")
	"execute gui-only commands here.
endif
\end{vimcode}
这就是用来检查用户使用的是 Gvim 还是 Vim 的所有代码. 需要注意的是, 仅仅检查
特性 \texttt{gui} 是否开启是不完整的, 因为如果 Vim 在编译时开启了 GUI 选项,
\texttt{has("gui")} 就会返回 \texttt{1} --- 即使当前并没有使用到图形界面.
\marginpar{183}

\begin{warning}
	执行 \texttt{:help 'feature-list'}, 可以查看 \texttt{has()} 支持的其他
	特性.
\end{warning}

\subsection{操作系统类型}
\label{subsec:which_operating_system}

如果脚本将要在多种不同的操作系统中运行, 比如 Microsoft Windows, Linux, 你将
会发现需要处理许多问题.

需要处理的问题包括程序的存放位置, 程序是否可用, 以及文件的访问权限.

有时候, 操作系统还会影响脚本的结构, 因为脚本可能会调用外部工具, 或访问依赖
于操作系统的功能.

Vim 允许脚本开发人员检查操作系统的类型, 这样的话, 脚本就可以根据操作系统的类型
作出相应的动作, 比如停止运行, 或进行特定的设置. 示例代码如下:
\begin{vimcode}
if has("win16") || has("win32") || has("win64") || has("win95")
	" do windows things here
elseif has("unix")
	" do linux/unix things here
endif
\end{vimcode}
上面的示例只展示了如何检查 Windows 与 Linux/Unix. 除了这些, 还可以检查其他类型
的操作系统, Vim 支持的操作系统类型可以用下面的命令找到:
\begin{vimcode}
:help 'feature-list'
\end{vimcode}

\subsection{Vim 的版本}
\label{subsec:which_version_of_vim}

在最近这二十年, Vim 的快速发展添加了许多新功能, 有时候, 开发人员会在脚本中使用
最新的函数, 因为它们使用起来最方便. 但是如果用户的 Vim 版本比较老, 没有提供
相应的函数, 那又该怎么办呢?
\marginpar{184}
面对这个问题有三种做法:
\begin{enumerate}
    \item 什么都不管, 让用户烦恼去吧 (不是个好主意)
    \item 检查用户所用的 Vim 是否是旧版, 如果是的话, 就停止加载脚本
    \item 检查用户所用的 Vim 是否过旧, 如果是的话, 就执行备用代码
\end{enumerate}

第一个选择实在不是个好主意, 所以我不推荐任何人使用它.

如果脚本无法处理旧版 Vim, 那么第二个选择是可接受的. 可是, 如果对于旧版有备选
方案可供选择, 那就最好用上.

现在讨论如何检查 Vim 的版本.

在检查 Vim 的版本之前, 我们得先看一下版本号的结构.

版本号由三部分组成:
\begin{itemize}
    \item 主版本号 (比如 Vim 7.2 中的 7)
    \item 次版本号 (比如 Vim 7.2 中的 2)
    \item 补丁号 (比如 Vim 7.2.103 中的 103)
\end{itemize}
前两个数字是真正的版本号, 当某个补丁或小的特性被应用到 Vim 的某个版本上时,
只有补丁号会发生变化. 只有当修改足够多时, 次版本号才会发生变化, 只有当 Vim
的主要部分发生变化时, 主版本号才会发生变化.

因此, 如果需要检查用户所使用的 Vim 版本, 就要检查这三个数字. 检查的示例代码是:
\begin{vimcode}
if v:version >= 702 || v:version == 701 && has("patch123")
    " code here is only for version 7.1 with patch 123
    " and version 7.2 and above
endif
\end{vimcode}
\texttt{if} 的第一个判断是检查 Vim 的版本是否大于或等于 7.2 (注意, 如果次版本
号小于 10, 就在前补 0). 如果第一个判断不满足, 就检查版本号是不是 7.1, 并且
打上了编号为 123 的补丁. 如果补丁号大于或等于 124, 那就表示补丁 123 已经应用
在了 Vim 上.
\marginpar{185}

\subsection{打印很长的行}
\label{subsec:printing_longer_lines}

Vim 最初是用在比较老式的文本终端上, 这些终端每一行的长度都不会超过某个限定值,
如今大多数的终端已经不再限制一行的长度, 但是这个限制在 Vim 中仍然时有出现.

如果使用 \texttt{echo} 语句往屏幕打印比较长的行时, 这个限制就会对打印行为产生
影响. 即使运行 Vim 的终端宽度大于 80 个字符, Vim 仍然会在打印完 80 个字符后
提醒用户按下回车键, 然后接着打印后面的字符. 有一个办法可以解决这个问题, 使得
在回显字符时, 能够用上全部的终端宽度. Vim 窗口列数减 1 后就是窗口的宽度,
比如, 如果 Vim 窗口的列数是 120 个字符, 那么窗口的宽度就是 119 个字符.

下面的函数用于打印长度为屏幕宽度的一行:
\begin{vimcode}
" WideMsg() prints [long] message up to (&columns-1) length
function! WideMsg(msg)
    let x=&ruler | let y=&showcmd
    set noruler noshowcmd
    redraw
    echo a:msg
    let &ruler=x | let &showcmd=y
endfunction
\end{vimcode}

\begin{warning}
    这个函数最早出现在 Yakov Lerner 所开发的 Vim 脚本中, 该脚本可以在
    \url{http://www.vim.org} 上找到.
\end{warning}

现在, 如果你需要在脚本中回显一行比较长的行, 可以使用函数 \texttt{WideMsg()},
使用的方式是:
\begin{vimcode}
:call WideMsg("This should be a very long line of text")
\end{vimcode}
一行的长度仍然受到限制, 但上限值并非原来的 79 个字符, 而是 Vim 窗口的宽度.
\marginpar{186}

\section{调试 Vim 脚本}
\label{sec:debugging_vim_scripts}

有时候, 脚本并不会按照开发人员期望中的那样工作, 在这种情况下, 开发人员就得
知道如何调试 Vim 脚本.

在这一节, 我们将会介绍几种寻找错误的方法.

\begin{warning}
    结构良好的脚本拥有更少的错误, 也更容易调试.
\end{warning}

Vim 提供了一种用于调试的特殊模式, 根据目标的不同, 启动该模式的方法也有所不同.

如果 Vim 抛出了一些错误 (在 Vim 窗口的底部打印它们), 但是开发人员并不确定发生
错误的地方, 也不知道为什么会发生这些错误, 这时候可以以调试方式直接启动 Vim.
方式是带上参数 \texttt{-D}:
\begin{vimcode}
vim -D somefile.txt
\end{vimcode}
当 Vim 开始读取第一个 \texttt{vimrc} 文件 (大部分情况下是安装路径中的全局
\texttt{vimrc} 文件) 时启动调试模式.

另一种需要进入调试模式的情况是开发人员已经知道某个函数存在错误, 因此他只想
调试这个函数. 对于这种情况可以以普通方式启动 Vim (必要的话, 还要加载包含待
调试函数的脚本), 然后执行:
\begin{vimcode}
:debug call Myfunction()
\end{vimcode}
\texttt{:debug} 后面的所有东西都是待调试的内容. 在上面的代码中只是调试函数
\texttt{Myfunction()} 的调用, 除此之外, 还可以写成:
\begin{vimcode}
:debug read somefile.txt
:debug nmap ,a :call Myfunction() <CR>
:debug help :debug
\end{vimcode}

接下来我们来看一下在调试模式下, 我们可以执行哪些操作.
\marginpar{187}

当执行到第一行需要调试的代码时, Vim 就停止加载, 显示一些类似于下面的信息:
\begin{vimcode}
Entering Debug mode. Type "cont" to continue
cmd: call MyFunction()
>
\end{vimcode}
进入到 Vim 脚本调试器后, 可以对 Vim 发出一些指令, 告诉它接下来如何操作.
\begin{warning}
    如果用户对调试技术不是很熟悉, 最好在开始调试脚本之前先把这一节看完.
\end{warning}

在调试器内可以使用下面的命令 (括号里面的是缩写形式):
\begin{itemize}
    \item \texttt{cont} (\texttt{c}): 按照正常方式继续执行脚本/命令, 直到下
        一个断点
    \item \texttt{quit} (\texttt{q}): 马上退出调试过程
    \item \texttt{interrupt} (\texttt{i}): 停止当前过程, 就像 \texttt{quit}
        那样, 但是回到调试模式中
    \item \texttt{step} (\texttt{s}): 执行下一行代码, 执行完毕后回到调试模式.
        如果下一行是函数调用或执行另一个文件中的命令, 则单步跟踪函数调用/命令
        执行
    \item \texttt{next} (\texttt{n}): 执行下一行代码, 执行完毕后回到调试模式,
        如果下一行是函数调用, 则会在函数返回后再回到调试模式
    \item \texttt{finish} (\texttt{f}): 继续执行, 即使遇到断点也不停止, 执行
        结束后回到调试模式.
\end{itemize}

通过这些命令, 开发人员就可以知道脚本/函数如何执行. 如果想要重复运行上一次运行
的命令, 只需要按下回车键.

如果需要的话, 可以在任意时刻执行另一个 \texttt{Ex} 命令 (参考 \texttt{:help
'ex-command-index'}\footnote{应该是个笔误 --- 译者注}), 但是需要注意的是, 在
调试器内无法直接访问变量, 除非它们是全局的.

有时候, 开发人员想要查看的地方在很多行代码的后面, 而他不想一步一步地执行代码,
那样需要花很长的时间, 也非常繁琐.
\marginpar{188}

对于这种情况, 开发人员可以在需要停止的地方插入一个断点, 然后在开始时执行
\texttt{cont}. 插入断点的命令有以下三种, 具体使用哪一种取决于开发人员的实际
需求:
\begin{vimcode}
:breakadd func linenum functionname
:breakadd file linenum filename
:breakadd here
\end{vimcode}

第一个命令把断点插入在特定的函数入口. 函数名 \texttt{functionname} 可以是模式,
比如 \texttt{Myfunction*}, 此时断点会插入在所有的, 其函数名以
\texttt{MyFunction} 开始的函数的入口.

有时候, 出现问题的地方并不是某个特定的函数, 而是文件中某一行的附近. 在这种情况
下应该使用第二条命令, 它在文件的某一行插入断点.

如果开发人员已经跟踪到某个位置, 并且希望当下次执行时在这里停住, 这时候就可以用
最后一个命令. 该命令在当前文件的当前行插入一个断点, 当然是在调试器内.

可以在任意时刻, 用下面的命令得到全部的断点:
\begin{vimcode}
:breaklist
\end{vimcode}

当断点不再需要时就可以把它删除, 和添加断点一样, 有若干种删除断点的方法.

最简单的一种是用命令 \texttt{:breaklist} 找到断点的编号, 然后用下命令删除它:
\begin{vimcode}
:breakdel number
\end{vimcode}

其他删除办法与添加断点的方法相同, 只不过要把 \texttt{breakadd} 改成
\texttt{breakdel}:
\begin{vimcode}
:breakdel func linenum functionname
:breakdel file linenum file
:breakdel here
\end{vimcode}

如果想要一次性删除全部的断点, 执行:
\begin{vimcode}
:breakdel *
\end{vimcode}
\marginpar{189}
